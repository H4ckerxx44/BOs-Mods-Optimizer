plugins {
  id 'fabric-loom' version '0.12-SNAPSHOT'
  id 'maven-publish'
  id 'idea'
  id 'com.matthewprenger.cursegradle' version '1.4.0'
  id 'nu.studer.credentials' version '3.0'
  id "com.modrinth.minotaur" version "2.+"
  id 'com.github.johnrengelman.shadow' version '7.1.2'
}

archivesBaseName = "${mod_id}-fabric-${minecraft_version}"

configurations {
  shade
  implementation.extendsFrom shade
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings loom.officialMojangMappings()
  modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

  implementation project(":Common")

  // Extra libraries
  shade 'com.moandjiezana.toml:toml4j:0.7.2'
  shade 'com.github.zafarkhaja:java-semver:0.9.0'
}

loom {
  runs {
    client {
      client()
      setConfigName("Fabric Client")
      ideConfigGenerated(true)
      runDir("run")
    }
    server {
      server()
      setConfigName("Fabric Server")
      ideConfigGenerated(true)
      runDir("run")
    }
  }
}

processResources {
  from project(":Common").sourceSets.main.resources
  inputs.property "version", project.version

  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

jar.enabled = false
shadowJar {
  finalizedBy 'remapJar'

  archiveClassifier = jar.archiveClassifier
  destinationDirectory = jar.destinationDirectory
  configurations = [project.configurations.shade]
  relocate 'com.moandjiezana.toml', "${project.group}.thirdparty.toml4j"
  relocate 'com.github.zafarkhaja', "${project.group}.thirdparty.semver"
  dependencies {
    include(dependency('com.moandjiezana.toml:toml4j:.*'))
    include(dependency('com.github.zafarkhaja:java-semver:.*'))
  }
  minimize()
}

remapJar {
  inputFile = shadowJar.archiveFile
  dependsOn shadowJar
}

tasks.withType(JavaCompile).configureEach {
  source(project(":Common").sourceSets.main.allSource)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.archivesBaseName
      version project.version
      from components.java
    }
  }

  repositories {
    maven {
      url "file://" + System.getenv("local_maven")
    }
  }
}

curseforge {
  apiKey = "${credentials.forKey('curseforge_api_token')}"
  project {
    id = "${curseforge_project_id}"
    changelog = file("../CHANGELOG.md")
    changelogType = 'markdown'
    releaseType = "${project.curseforge_release_type}"
    addGameVersion("${minecraft_version}")
    addGameVersion("Fabric")
    addGameVersion("Quilt")
    mainArtifact(remapJar)
  }
  options {
    forgeGradleIntegration = false
  }
}

modrinth {
  token = "${credentials.forKey('modrinth_api_token')}"
  projectId = "${modrinth_project_id}"
  versionNumber = "${version}"
  versionType = "${modrinth_release_type}"
  uploadFile = remapJar
  gameVersions = ["${minecraft_version}"]
  loaders = ["fabric", "quilt"]
}
